{ outputs, config, edwardh-dev, ... }:
{
  networking.hostName = "edwardh";
  networking.domain = "dev";

  imports = with outputs.nixosModules; [
    basicConfig
    fzf
    git
    headless
    homeManager
    monitoring
    ssh
    users
    zsh

    (builtins.fetchTarball {
      # nixos-unstable as of 2025-04-07
      url = "https://gitlab.com/simple-nixos-mailserver/nixos-mailserver/-/archive/b4fbffe79c00f19be94b86b4144ff67541613659/nixos-mailserver-b4fbffe79c00f19be94b86b4144ff67541613659.tar.gz";
      sha256 = "0r8c0mkj7cn2cz0r6m45h51w5qwf2cyiiv956bz75p3fcps4qj1n";
    })
  ];

  age.secrets.mail-hashed-password.file = ../../secrets/mail-hashed-password.age;
  age.secrets.radicale-htpasswd = {
    file = ../../secrets/radicale-htpasswd.age;
    owner = "radicale";
    group = "radicale";
    mode = "400";
  };
  age.secrets.wg0-edwardh-key.file = ../../secrets/wg0-edwardh-key.age;
  age.secrets.wg1-edwardh-key.file = ../../secrets/wg1-edwardh-key.age;
  age.secrets.wg2-edwardh-key.file = ../../secrets/wg2-edwardh-key.age;
  age.secrets.wg0-gateway-preshared-key.file = ../../secrets/wg0-gateway-preshared-key.age;
  age.secrets.wg1-gateway-preshared-key.file = ../../secrets/wg1-gateway-preshared-key.age;
  age.secrets.wg2-gateway-preshared-key.file = ../../secrets/wg2-gateway-preshared-key.age;
  age.secrets.wg1-edward-dell-01-preshared-key.file = ../../secrets/wg1-edward-dell-01-preshared-key.age;
  age.secrets.wg2-edward-dell-01-preshared-key.file = ../../secrets/wg2-edward-dell-01-preshared-key.age;

  networking.firewall.interfaces."wg0".allowedTCPPorts = [ 9002 9004 9005 9006 ]; # Prometheus exporters

  networking.firewall.allowedTCPPorts = [
    80 # HTTP
    443 # HTTPS
    53 # DNS
    4243 # Automx2
    22 # SSH
  ];
  networking.firewall.allowedUDPPorts = [
    53 # DNS
    51800 # wg0
    51801 # wg1
    51802 # wg2
  ];

  # Manually set DNS nameservers, here we use my local DNS server.
  environment.etc."resolv.conf".text = ''
    # Generated by NixOS
    domain dev
    search dev eu-west-2.compute.internal
    nameserver 172.16.10.1
    options edns0 trust-ad
  '';

  services.roundcube = {
    enable = true;
    # Web interface accessible from hostName.
    hostName = "mail.edwardh.dev";
    extraConfig = ''
      $config['smtp_server'] = "tls://${config.mailserver.fqdn}";
      $config['smtp_user'] = "%u";
      $config['smtp_pass'] = "%p";
    '';
  };

  mailserver = {
    enable = true;
    localDnsResolver = false;

    fqdn = "mail.edwardh.dev";
    sendingFqdn = "edwardh.dev";
    domains = [ "edwardh.dev" ];

    backup.enable = true; # Backup to /var/rsnapshot

    loginAccounts = {
      "inbox@edwardh.dev" = {
        # nix-shell -p mkpasswd --run 'mkpasswd -sm bcrypt'
        hashedPasswordFile = config.age.secrets.mail-hashed-password.path;
        aliases = [ "@edwardh.dev" ];
        sieveScript = ''
          require ["fileinto", "mailbox"];

          if address :contains "To" "airtable" {
            fileinto "Organizations.Airtable";
            stop;
          }      
          if address :contains "To" "apple" {
            fileinto "Organizations.Apple";
            stop;
          }
          if address :contains "To" "bsky" {
            fileinto "Organizations.Bluesky";
            stop;
          }
          if address :contains "From" "github.com" {
            fileinto "Organizations.GitHub";
            stop;
          }
          if address :contains "To" "google" {
            fileinto "Organizations.Google";
            stop;
          }
          if address :contains "To" "hackclub" {
            fileinto "Organizations.Hack Club";
            stop;
          }
          if address :contains "To" "immobilise" {
            fileinto "Organizations.Immobilise";
            stop;
          }
          if address :contains "To" "itch" {
            fileinto "Organizations.Itch";
            stop;
          }
          if address :contains "To" "jlc" {
            fileinto "Organizations.JLCPCB";
            stop;
          }
          if address :contains "To" "lner" {
            fileinto "Organizations.LNER";
            stop;
          }
          if address :contains "To" "meta" {
            fileinto "Organizations.Meta";
            stop;
          }
          if address :contains "To" "modrinth" {
            fileinto "Organizations.Modrinth";
            stop;
          }
          if address :contains "To" "nasa" {
            fileinto "Organizations.NASA";
            stop;
          }
          if address :contains "From" "pcbway.com" {
            fileinto "Organizations.PCBWay";
            stop;
          }
          if address :contains "From" "pcbx.com" {
            fileinto "Organizations.PCBX";
            stop;
          }
          if address :contains "To" "prusa" {
            fileinto "Organizations.Prusa";
            stop;
          }
          if address :contains "To" "steam" {
            fileinto "Organizations.Steam";
            stop;
          }
          if address :contains "To" "thepihut" {
            fileinto "Organizations.ThePiHut";
            stop;
          }
          if address :contains "To" "abuseipdb" {
            fileinto "Organizations.AbuseIPDB";
            stop;
          }
          if address :contains "To" "obsidian" {
            fileinto "Organizations.Obsidian";
            stop;
          }
          if address :contains "To" "microsoft" {
            fileinto "Organizations.Microsoft";
            stop;
          }
          if address :contains "To" "ubisoft" {
            fileinto "Organizations.Ubisoft";
            stop;
          }

          if address :contains "To" "security" {
            fileinto "Security";
            stop;
          }
          if address :contains "To" "headblockhead" {
            fileinto "headblockhead";
            stop;
          }
        '';
      };
    };

    mailboxes = {
      # Special mailboxes
      Drafts = {
        auto = "subscribe";
        specialUse = "Drafts";
      };
      Junk = {
        auto = "subscribe";
        specialUse = "Junk";
      };
      Sent = {
        auto = "subscribe";
        specialUse = "Sent";
      };
      Trash = {
        auto = "subscribe";
        specialUse = "Trash";
      };
      Archives = {
        auto = "subscribe";
        specialUse = "Archive";
      };

      # Individual organizations, sorted by sieve scripts.
      "Organizations.AbuseIPDB" = { auto = "subscribe"; };
      "Organizations.Airtable" = { auto = "subscribe"; };
      "Organizations.Apple" = { auto = "subscribe"; };
      "Organizations.BAFTA" = { auto = "subscribe"; }; # not autosorted
      "Organizations.Bluesky" = { auto = "subscribe"; };
      "Organizations.GitHub" = { auto = "subscribe"; }; # sorted via sender
      "Organizations.Google" = { auto = "subscribe"; };
      "Organizations.Hack Club" = { auto = "subscribe"; };
      "Organizations.Immobilise" = { auto = "subscribe"; };
      "Organizations.Itch" = { auto = "subscribe"; };
      "Organizations.JLCPCB" = { auto = "subscribe"; };
      "Organizations.LNER" = { auto = "subscribe"; };
      "Organizations.Meta" = { auto = "subscribe"; };
      "Organizations.Microsoft" = { auto = "subscribe"; };
      "Organizations.Modrinth" = { auto = "subscribe"; };
      "Organizations.NASA" = { auto = "subscribe"; };
      "Organizations.Obsidian" = { auto = "subscribe"; };
      "Organizations.PCBWay" = { auto = "subscribe"; }; # sorted via sender
      "Organizations.PCBX" = { auto = "subscribe"; }; # sorted via sender
      "Organizations.Prusa" = { auto = "subscribe"; };
      "Organizations.Ubisoft" = { auto = "subscribe"; };
      "Organizations.Steam" = { auto = "subscribe"; };
      "Organizations.ThePiHut" = { auto = "subscribe"; };

      # Individual people
      "People" = { auto = "subscribe"; };

      # General categories
      "Shipping and Recipts" = { auto = "subscribe"; };
      "School" = { auto = "subscribe"; };
      "Performances" = { auto = "subscribe"; };
      "Music" = { auto = "subscribe"; };
      "Security" = { auto = "subscribe"; }; # LetsEncrypt

      # Forwarded from my old email address
      "headblockhead" = { auto = "subscribe"; };
    };

    certificateScheme = "acme-nginx";
  };

  services.automx2 = {
    enable = true;
    domain = "edwardh.dev";
    port = 4243;
    # https://rseichter.github.io/automx2/#_sqlite
    settings =
      {
        provider = "Edward Hesketh";
        domains = [ "edwardh.dev" ];
        servers = [
          { type = "imap"; name = "mail.edwardh.dev"; }
          { type = "smtp"; name = "mail.edwardh.dev"; }
          { type = "caldav"; port = 443; url = "https://calendar.edwardh.dev/SOGo/dav/%EMAILADDRESS%/Calendar/personal/"; }
          { type = "carddav"; port = 443; url = "https://contacts.edwardh.dev/SOGo/dav/%EMAILADDRESS%/Contacts/personal/"; }
        ];
      };
  };

  security.acme.acceptTerms = true;
  security.acme.defaults.email = "security@edwardh.dev";

  services.fail2ban = {
    enable = true;
    bantime = "8h";
    bantime-increment = {
      enable = true;
      rndtime = "30m";
      maxtime = "168h";
    };
    jails = {
      sshd.settings = {
        enabled = true;
        mode = "aggressive";
      };
      dovecot.settings = {
        enabled = true;
        mode = "aggressive";
      };
      postfix.settings = {
        enabled = true;
        mode = "aggressive";
      };
    };
  };

  # Store zones in /etc so they can be signed without errors due to trying to write to the store.
  systemd.tmpfiles.rules = [
    "d /etc/bind/zones 755 named root -"
    "L+ /etc/bind/zones/db.edwardh.dev - - - - ${./db.edwardh.dev}"
    "z /etc/bind/zones/db.edwardh.dev 744 named root -"
  ];

  services.bind = {
    enable = true;
    extraOptions = ''
      recursion no;
      allow-transfer { none; };
      version "not currently available";
    '';
    zones."edwardh.dev" = {
      master = true;
      file = "/etc/bind/zones/db.edwardh.dev";
      allowQuery = [ "any" ];
      # To get the DS record:
      # dig dnskey edwardh.dev | dnssec-dsfromkey -f - edwardh.dev
      extraConfig = ''
        dnssec-policy default;
        inline-signing yes;

        # ${./db.edwardh.dev}
        # The above comment is included so that the bind service will be restarted when the db.edwardh.dev file changes.
      '';
    };
  };

  services.prometheus.exporters.bind = {
    enable = true;
    port = 9004;
    listenAddress = "172.16.10.2";
  };

  services.radicale = {
    enable = true;
    settings = {
      server.hosts = [ "127.0.0.1:5232" ];
      auth = {
        type = "htpasswd";
        htpasswd_filename = config.age.secrets.radicale-htpasswd.path;
        htpasswd_encryption = "bcrypt";
      };
    };
  };

  networking.wireguard = {
    enable = true;
    interfaces = {
      wg0 = {
        ips = [ "172.16.10.2/24" ];
        listenPort = 51800;
        privateKeyFile = config.age.secrets.wg0-edwardh-key.path;
        peers = [
          {
            name = "gateway";
            publicKey = "1Gs85rAE+d++lojXtc04P448bXcZNdLZjIx/uWo0pSM=";
            allowedIPs = [ "172.16.0.0/12" ]; # edwardh can connect to server network
            presharedKeyFile = config.age.secrets.wg0-gateway-preshared-key.path;
          }
        ];
      };
      wg1 = {
        ips = [ "172.16.11.2/24" ];
        listenPort = 51801;
        privateKeyFile = config.age.secrets.wg1-edwardh-key.path;
        peers = [
          {
            name = "gateway";
            publicKey = "N/BghPeRn6f2FbeW7fhh1WR3dd5rdsirfM+Otplxu1k=";
            allowedIPs = [ "172.16.11.1/32" ]; # edwardh can connect to gateway
            presharedKeyFile = config.age.secrets.wg1-gateway-preshared-key.path;
          }
          #          {
          #name = "edward-laptop-01";
          #publicKey = "";
          #allowedIPs = [ "172.16.11.10/32" ];
          #presharedKeyFile = config.age.secrets.wg1-edward-laptop-01-preshared-key.path;
          #}
          {
            name = "edward-dell-01";
            publicKey = "JM26AmQkP0ZmPme7PU4kB8bUAEgIPovPxxlC4fJAK00=";
            allowedIPs = [ "172.16.11.11/32" ];
            presharedKeyFile = config.age.secrets.wg1-edward-dell-01-preshared-key.path;
          }
        ];
      };
      wg2 = {
        ips = [ "172.16.12.2/24" ];
        listenPort = 51802;
        privateKeyFile = config.age.secrets.wg2-edwardh-key.path;
        peers = [
          {
            name = "gateway";
            publicKey = "wktxkYndiWThccdNLRXmaFYupDP6Yhb+J584zgz1u2Y=";
            allowedIPs = [ "172.16.12.1/32" ]; # edwardh can connect to gateway
            presharedKeyFile = config.age.secrets.wg2-gateway-preshared-key.path;
          }
          #          {
          #name = "edward-laptop-01";
          #publicKey = "";
          #allowedIPs = [ "172.16.12.10/32" ];
          #presharedKeyFile = config.age.secrets.wg2-edward-laptop-01-preshared-key.path;
          #}
          {
            name = "edward-dell-01";
            publicKey = "GfqLz2aiUIYy5U+m6QpKhXfV9LqZLcsamZTSUJwMdwo=";
            allowedIPs = [ "172.16.12.11/32" ];
            presharedKeyFile = config.age.secrets.wg2-edward-dell-01-preshared-key.path;
          }
        ];
      };
    };
  };

  services.prometheus.exporters.wireguard = {
    enable = true;
    port = 9006;
    listenAddress = "172.16.10.2";

    withRemoteIp = true; # Show the remote IP address of the peer
  };

  services.nginx = {
    enable = true;
    statusPage = true; # localhost only, used by prometheus exporter
    virtualHosts = {
      "edwardh.dev" = {
        default = true;
        forceSSL = true;
        enableACME = true;
        locations = {
          "/" = {
            root = edwardh-dev.packages.edwardh-dev;
            extraConfig = ''
              gzip on;
            '';
          };
          "/.well-known/matrix/server" = {
            extraConfig = ''
              default_type application/json;
              return 200 '{ "m.server": "matrix.edwardh.dev:443" }';
            '';
          };
          "/.well-known/matrix/client" = {
            extraConfig = ''
              default_type application/json;
              return 200 '{ "m.homeserver": { "base_url": "https://matrix.edwardh.dev" } }';
              add_header "Access-Control-Allow-Origin" *;
            '';
          };
        };
      };
      "calendar.edwardh.dev" = {
        forceSSL = true;
        enableACME = true;
        serverAliases = [ "contacts.edwardh.dev" ];
        locations."/" = {
          proxyPass = "http://127.0.0.1:5232";
          recommendedProxySettings = true;
        };
      };
      # Local services
      "cache.edwardh.dev" = {
        forceSSL = true;
        enableACME = true;
        locations."/" = {
          proxyPass = "http://172.16.3.51"; # rpi5-01
          recommendedProxySettings = true;
        };
      };
      "grafana.edwardh.dev" = {
        forceSSL = true;
        enableACME = true;
        locations."/" = {
          proxyPass = "http://172.16.3.1:3000"; # gateway
          proxyWebsockets = true;
          recommendedProxySettings = true;
        };
      };
      "hass.edwardh.dev" = {
        forceSSL = true;
        enableACME = true;
        locations."/" = {
          proxyPass = "http://172.16.3.41:8123"; # rpi4-01
          proxyWebsockets = true;
          recommendedProxySettings = true;
        };
      };
      "matrix.edwardh.dev" = {
        forceSSL = true;
        enableACME = true;
        locations."/_matrix" = {
          proxyPass = "http://172.16.3.51:8008"; # rpi5-01
          recommendedProxySettings = true;
        };
      };
    };
  };

  services.prometheus.exporters.nginx = {
    enable = true;
    port = 9005;
    listenAddress = "172.16.10.2";
  };

  environment.systemPackages = [
  ];

  security.sudo.wheelNeedsPassword = false;

  system.stateVersion = "22.05";
}
